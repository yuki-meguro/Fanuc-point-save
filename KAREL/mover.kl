--* coding: Shift-jis *--
--
--  --------------------------------------
--  Released under the Apache 2.0 License.
--  Yuki Meguro
--  https://github.com/yuki-meguro
--  --------------------------------------
--
--  --------------------------------------
--  Author: Yuki Meguro
--
--  Description:
--
--  Dependancies: point_load.kl
--  --------------------------------------


ROUTINE frame_set(mo_data: rtrn1): INTEGER
VAR
    motype_no,
    uframe_no,
    utool_no: INTEGER

    stat: INTEGER
BEGIN
    motype_no = mo_data.list[1]
    uframe_no = mo_data.list[2]
    utool_no = mo_data.list[3]

    IF uframe_no = 0 THEN
        $UFRAME = $MOR_GRP[1].$NILPOS
    ELSE
        $UFRAME = $MNUFRAME[1, uframe_no] 
    ENDIF
    $UTOOL = $MNUTOOL[1, utool_no]

    RETURN(motype_no)
END frame_set

ROUTINE load_posdata: XYZWPR
VAR
    stat: INTEGER
    real_flag: BOOLEAN
    get_int: INTEGER
    trash_real: REAL
    point_no,
    target_no: INTEGER
    point: XYZWPR
BEGIN
    GET_REG(point_reg, real_flag, get_int, trash_real, stat)
    point_no = get_int
    target_no = preg_begin + point_no - 1
    point = GET_POS_REG(target_no, stat) 
    RETURN(point)
END load_posdata

ROUTINE move_set(motion: INTEGER)
BEGIN
	--settings
    $MCR.$GENOVERRIDE = 30
    $SPEED = 250
    $TERMTYPE = FINE

    SELECT motion OF
        CASE(10):
            $MOTYPE = JOINT
        CASE(20):
            $MOTYPE = LINEAR
        ELSE:
            WRITE(CHR(137),CHR(128))
            FORCE_SPMENU(TP_PANEL,SPI_TPUSER,1)
            WRITE('Motion Type Err.', CR)
            ABORT
    ENDSELECT
END move_set

ROUTINE move_it(point: XYZWPR): INTEGER
VAR
    stat: INTEGER
    real_flag: BOOLEAN
    get_int: INTEGER
    trash_real: REAL
    ans: INTEGER
BEGIN
    MOVE TO point
    GET_REG(point_reg, real_flag, get_int, trash_real, stat)
    ans = get_int - 1
    IF ans > 0 THEN
        SET_INT_REG(point_reg, ans, stat)
        RETURN(0)
    ELSE
        RETURN(1)
    ENDIF
END move_it

ROUTINE sub1
VAR
    loop: BOOLEAN
    mo_stat: INTEGER
    motype_no: INTEGER
    point_: XYZWPR
BEGIN
    loop = TRUE
    WHILE loop DO
        motype_no = frame_set(mo_data)
        move_set(motype_no)
        point_ = load_posdata
        mo_stat = move_it(point_)
        IF mo_stat = 1 THEN
            loop = FALSE
        ENDIF
    ENDWHILE
END sub1